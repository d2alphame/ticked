= Ticked


== This is H2

Ticked is a markdown-like lightweight markup language. But why, you may ask.
Why would you create yet another dialect of markdown? The answer is simple
really because I want to, and because I can. :D


= Headers

Headers are prefixed with the equals sign (=) followed by at least 1 whitespace.
The number of equals sign determines the heading level. For example:

= This is H1


== This is H2


=== This is H3

Also note that headers are surrounded by blank lines.


== Paragraphs

A paragraph is a group of non-blank lines separated by blank lines. This, for
example, forms a paragraph.

This also forms a paragraph.

And this is also another paragraph.


== Escaping characters

Use the apostrophe to escape ticked formatting characters. Escape the asterisk
with '*, escape the slash with '/, escape the backticks with '`, etc.


== Inline elements

Surround with asterisks for strong, surround with slash for em, surround with
backticks for code and surround with underscores to underline. For example
This *word* becomes strong, /this/ one becomes em, _this_ becomes underline and
`this` becomes code.


=== URLs

Urls are specified in square brackets like [proto://domain.tld]. When stated in
this form, the domain part of the url becomes the url's text. If there's a 
custom text to specify for the url, use square brackets
like [this example| https://example.com].

The url could be relative, meaning it is a path in the current website. For this
just use the 'path' part of the url [like this| /path/to/file.ext]
or [this| another/path/file.ext]

When only the fragment is specified, then the fragment points to a section of
the current page. For example [go to the section about urls| #url].

Any word within a paragraph or header can be used to 'id' said paragraph or
header. This is done by preceding the id with a hash (`#`) symbol. For example,
this paragraph is about linking a #url. This paragraph can then be linked to as a
page fragment like this: [linking urls| #url].

If the word to be used as id for the paragraph or header does not appear in the
paragraph or header, append the word to the end of the paragraph (or header)
with double hashes (`##`) -  like this paragraph. ##fragments

Urls can be linked to in reference style. For example,
visit [King Bumi Archives]. This means there's reference in the reference
section that looks like this:
```
1. King Bumi Archives| ftps://bumi.msh.ek/archives
```
Another way to link with that same reference would be something like [this|1].
(See [how to write references])


=== Verbatim

Ticked uses braces to stop processing ticked markup characters. Anything between
an opening brace and a closing brace is not ticked-processed and will appear
as-is.
For example { everything here is closed in braces, and will appear as-is. *
appears as asterisk, / appears as slash etc. }


=== HTML

Raw HTML can be used in ticked.  


=== A note on formatting markers

Besides being escaped and appearing in verbatim elements, there are other times
when formatting markers appear as is.

An opening marker followed immediately by whitespace will not have formatting
applied. For example * this will not become strong*, / this will not become
em/ and ` this will not become code`. Notice the whitespace after the opening
marker in each case.

A closing marker preceded immediately by a whitespace also doesn't get
formatted.  
This *will not become strong *. Note the whitespace preceding the closing
marker.  
This /will not become em /. Again, note the whitespace preceding the closing
marker.

Two consecutive markers such as ** or // or `` etc will also have no effect and
will appear as is.

== Blockquotes

Each line of a blockquote begins with a tab or space and ends with a line that
begins with a tab or space followed by double dashes. For example:

    Water, Earth, Fire, Air.  Long ago, the four nations lived together in
    harmony. Then, everything changed when the Fire Nation attacked. Only the
    Avatar, master of all four elements, could stop them. But when the world
    needed him most, he vanished. A hundred years passed and my brother and I
    discovered the new Avatar, an Airbender named Aang. And though his
    airbending skills are great, he has a lot to learn before he's ready to save
    anyone. But I believe Aang can save the world.
    -- Katara, ATLA

The last line is the citation for the quote and the source is /not/ necessary.
So, that could just have been just the double dashes (which is necessary).

Here's another example of a blockquote without the citing the source. Note the
double dashes at the end that marks the end of the quote

    I don't know the answer. Sometimes life is like this dark tunnel. You can't
    always see the light at the end of the tunnel. But if you just keep moving,
    you will come to a better place.
    --

== Lists

For ordered lists, use the # symbol for each item in the list.

# Ozai
# Iroh
# Azula
# Zuko

For unordered lists, use the + symbol for each item.

+ Water
+ Earth
+ Fire
+ Air

Definition lists are written as follows:

- Water Tribe:
  The water tribe consists of 2 tribes - The Northern Water Tribe and the
  Southern Water Tribe. Katara is from the Southern Water Tribe.

- Fire Nation:
  This is the fiercest of the 4. The Fire Nation genocided the Air Nomads and
  started the 100 year war.

- Earth Kingdom:
  The Earth Kingdom is the biggest. Omashu and Ba Sing Se are parts of the
  Earth Kingdom.

- Air Nomads:
  Air Nomads are a nomadic tribe. Aang, is from this tribe and was raised by
  monks. 


== Code fences

Blocks of code, (or even code-like blocks) can be added between a pair of lines
that begin with {```}, that is 3 backtick characters. For example
```
for $char(@benders) {
    say $char{element}
}
```


== Admonitions and Sidebars

Admonitions are added between a pair of lines that begin with 3 apostrophes. The
type of admonition is stated alongside the opening markers. 4 types of
admonitions recognized by ticked are 'Tip', 'Note', 'Caution', and 'Warning'.
Here are examples:
'''Tip
You need to have a stubborn character to do earthbending right.
'''

Here's an example of a note:
'''Note
Everyone needs to visit Wan Shi Tong's library at least once in their life.
'''

A caution:
'''Caution
Momo is the deadliest member of Team Avatar.
'''

A warning:
'''Warning
Never piss off an Avatar in any interactions you may have with them.
'''

A sidebar is exactly the same except without admonition labels. What follows is
an example
'''
The bending arts of the Avatar world are based on real life Martial arts.
'''


== Horizontal rules and line breaks

3 consecutive dashes on a line by itself makes a horizontal rule.
---
End a line with at least 2 spaces (could be more) to insert a hard line break.
In html that would be {<br />}.  
This would be on one line, it ends with 2 spaces.  
This would be on a different line from the previous one.


== Writing #references

The reference section appears at the end of the file. It is marked
by `:: References`, and does *not* appear in the final output
of the file. Here's what the references section of this file looks like:
```
:: References
1. King Bumi Archives| ftps://bumi.msh.ek/archives
2. how to write references| #references
```

:: References
1. King Bumi Archives| https://bumi.msh.ek/archives
2. how to write references| #references
